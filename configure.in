dnl -*- tab-width: 2; indent-tabs-mode: nil -*-

AC_INIT(src/libredcarpet.h)

AM_CONFIG_HEADER(config.h)

dnl libtool versioning
LT_CURRENT=0
LT_REVISION=0
LT_AGE=0

VERSION=$LT_CURRENT.$LT_REVISION.$LT_AGE
PACKAGE=libredcarpet

AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)
AC_SUBST(LT_VERSION)
AC_SUBST(LT_PACKAGE)

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

dnl Check for required programs
AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AC_ARG_PROGRAM
AC_PROG_LIBTOOL

dnl Check for libz to handle compressed xml files
AC_CHECK_LIB(z, inflate)

dnl The available backends
AC_ARG_ENABLE(backend-rpm3, [  --enable-backend-rpm3   include RPMv3 backend],
  enable_backend_rpm3=yes, enable_backend_rpm3=no)
AC_ARG_ENABLE(backend-rpm4, [  --enable-backend-rpm4   include RPMv4 backend],
  enable_backend_rpm4=yes, enable_backend_rpm4=no)
AC_ARG_ENABLE(backend-dpkg, [  --enable-backend-dpkg   include DPKG backend],
  enable_backend_dpkg=yes, enable_backend_dpkg=no)

if test "x$enable_backend_rpm3" != "xyes" -a "x$enable_backend_rpm4" != "xyes" -a "x$enable_backend_dpkg" != "xyes";
then
  AC_WARN([no backends were specified])
fi

if test "x$enable_backend_rpm3" = "xyes" -a "x$enable_backend_rpm4" = "xyes";
then
  AC_ERROR([rpm3 and rpm4 may not be concurrently used])
fi

AC_ARG_WITH(rpm-prefix,
[  --with-rpm-prefix=DIR   prefix where rpm header files are installed (/usr)],
,[with_rpm_prefix=/usr])

dnl RPM (any version)
RPM_PREFIX="$with_rpm_prefix"
AC_SUBST(RPM_PREFIX)

AM_CONDITIONAL(ENABLE_RPM, false)
ENABLE_RPM=no
AC_SUBST(ENABLE_RPM)

dnl RPMv3
if test "x$enable_backend_rpm3" = "xyes"; then
  save_CPPFLAGS="$CPPFLAGS"
  CPPFLAGS="$CPPFLAGS -I$RPM_PREFIX/include/rpm"

  AC_CHECK_LIB(z, gzopen,
    AC_CHECK_HEADERS(zlib.h,, AC_MSG_ERROR(couldn't find libz headers)),
      AC_MSG_ERROR(couldn't find libz))
  AC_CHECK_LIB(popt, poptGetArgs,
    AC_CHECK_HEADERS(popt.h,, AC_MSG_ERROR(couldn't find libpopt headers)),
      AC_MSG_ERROR(couldn't find libpopt))
  AC_CHECK_LIB(rpm, rpmdbOpen,
    AC_CHECK_HEADERS(rpm/rpmlib.h,, AC_MSG_ERROR(couldn't find rpmlib.h)),
      AC_MSG_ERROR(couldn't find librpm), -lz -lpopt)
  AC_SEARCH_LIBS(db_version, db1 db db-3.1,,
    AC_MSG_ERROR(couldn't find libdb))

  RPM_BZ2_P=`nm --undefined-only $RPM_PREFIX/lib/librpm.a | grep bzopen`

  if test "xRPM_BZ2_P" != "x"; then
    RPM_BZ2="-lbz2"
  else
    RPM_BZ2=""
  fi

  RPMLIB_LIBS="-L$RPM_PREFIX/lib -Wl,-Bstatic -lrpm -lpopt -Wl,-Bdynamic -lz $RPM_BZ2"
  RPMLIB_CFLAGS="-I$RPM_PREFIX/include/rpm"

  AM_CONDITIONAL(ENABLE_RPM, true)
  ENABLE_RPM=yes
  AC_DEFINE(ENABLE_RPM)

  AC_SUBST(RPMLIB_LIBS)
  AC_SUBST(RPMLIB_CFLAGS)

  CPPFLAGS="$save_CPPFLAGS"
fi

dnl RPMv4
if test "x$enable_backend_rpm4" = "xyes"; then
  save_CPPFLAGS="$CPPFLAGS"
  CPPFLAGS="$CPPFLAGS -I$RPM_PREFIX/include/rpm"

  AC_CHECK_LIB(z, gzopen,
    AC_CHECK_HEADERS(zlib.h,, AC_MSG_ERROR(couldn't find libz headers)),
      AC_MSG_ERROR(couldn't find libz))
  AC_CHECK_LIB(bz2, BZ2_bzopen,
    AC_CHECK_HEADERS(bzlib.h,, AC_MSG_ERROR(couldn't find libbz headers)),
      AC_MSG_ERROR(couldn't find libbz))
  AC_CHECK_LIB(popt, poptGetArgs,
    AC_CHECK_HEADERS(popt.h,, AC_MSG_ERROR(couldn't find libpopt headers)),
      AC_MSG_ERROR(couldn't find libpopt))
  AC_CHECK_LIB(rpmio, Fopen,
    AC_CHECK_HEADERS(rpm/rpmio.h,, AC_MSG_ERROR(couldn't find rpmio.h)),
      AC_MSG_ERROR(couldn't find librpmio), -lz -lbz2 -lpopt)
  AC_CHECK_LIB(rpm, rpmdbOpen,
    AC_CHECK_HEADERS(rpm/rpmlib.h,, AC_MSG_ERROR(couldn't find rpmlib.h)),
      AC_MSG_ERROR(couldn't find librpm), -lz -lbz2 -lpopt -lrpmio)
  if grep -q ugid.h "$RPM_PREFIX/include/rpm/misc.h"; then
    AC_MSG_ERROR(broken misc.h, see README-ugid)
  fi
  AC_CHECK_LIB(db-3.1, db_version,,, -ldb1) dnl what?
  RPMLIB_LIBS="-L$RPM_PREFIX/lib -Wl,-Bstatic -lrpm -lrpmio -lpopt -Wl,-Bdynamic -lz -lbz2"
  RPMLIB_CFLAGS="-I$RPM_PREFIX/include/rpm"

  AM_CONDITIONAL(ENABLE_RPM, true)
  ENABLE_RPM=yes
  AC_DEFINE(ENABLE_RPM)

  AC_SUBST(RPMLIB_LIBS)
  AC_SUBST(RPMLIB_CFLAGS)

  AC_DEFINE(HAVE_RPM_4_0)

  CPPFLAGS="$save_CPPFLAGS"
fi

AM_CONDITIONAL(ENABLE_DPKG, false)
ENABLE_DPKG=no
AC_SUBST(ENABLE_DPKG)

dnl DPKG
if test "x$enable_backend_dpkg" = "xyes"; then
  AC_ARG_ENABLE(fake-openpty,
    [  --enable-fake-openpty   pretend openpty exists],
    [CFLAGS="$CFLAGS -D'openpty(x)=/**/'"; disable_openpty=yes])
  if test "x$disable_openpty" != "xyes"; then
    AC_CHECK_LIB(util, openpty,,
      AC_MSG_ERROR(couldn't find openpty in libutil))
  fi

  AM_CONDITIONAL(ENABLE_DPKG, true)
  ENABLE_DPKG=yes
  AC_DEFINE(ENABLE_DPKG)
fi

dnl libxml
XML_LIBS=`xml-config --libs`
XML_CFLAGS=`xml-config --cflags`

if test "x$XML_LIBS" = "x"; then
  AC_MSG_ERROR(couldn't find xml-config)
fi

save_CPPFLAGS="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $XML_CFLAGS"
save_LDFLAGS="$LDFLAGS"
LDFLAGS="$LDFLAGS $XML_LIBS"

AC_CHECK_LIB(xml, xmlDocDump,,
  AC_MSG_ERROR(couldn't find libxml))

AC_CHECK_HEADERS(tree.h,,
  AC_MSG_ERROR(couldn't find tree.h))

CPPFLAGS="$save_CPPFLAGS"
LDFLAGS="$save_LDFLAGS"

dnl gtk
AM_PATH_GTK(1.2.0)

dnl glib???
AM_PATH_GLIB(1.2.0)

AC_OUTPUT([
Makefile
src/Makefile
])

echo "
Configuration:

  Source code location:   ${srcdir}
  Compiler:               ${CC}
  Include RPM3 support:   ${enable_backend_rpm3}
  Include RPM4 support:   ${enable_backend_rpm4}
  Include DPKG support:   ${enable_backend_dpkg}
"
